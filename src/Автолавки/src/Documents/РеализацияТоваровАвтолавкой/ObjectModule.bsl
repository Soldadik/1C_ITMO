
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровАвтолавкойСписокТоваров.Товар КАК Товар,
		|	СУММА(РеализацияТоваровАвтолавкойСписокТоваров.Количество) КАК Количество
		|ПОМЕСТИТЬ ДокТЧ
		|ИЗ
		|	Документ.РеализацияТоваровАвтолавкой.СписокТоваров КАК РеализацияТоваровАвтолавкойСписокТоваров
		|ГДЕ
		|	РеализацияТоваровАвтолавкойСписокТоваров.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровАвтолавкойСписокТоваров.Товар
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокТЧ.Товар КАК Товар,
		|	ДокТЧ.Количество КАК Количество
		|ИЗ
		|	ДокТЧ КАК ДокТЧ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Движения.ТоварыВАвтолавке.Записывать = Истина;
	Движения.ТоварыВАвтолавке.БлокироватьДляИзменения = Истина;
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ТоварыВАвтолавке.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Товар = Выборка.Товар;
		Движение.Количество = Выборка.Количество;
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыВАвтолавкеОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ТоварыВАвтолавкеОстатки.Товар.Представление КАК ТоварПредставление
		|ИЗ
		|	РегистрНакопления.ТоварыВАвтолавке.Остатки(&Дата, Товар В(ВЫБРАТЬ ДокТЧ.Товар ИЗ ДокТЧ) ) КАК ТоварыВАвтолавкеОстатки
		|ГДЕ
		|	ТоварыВАвтолавкеОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Дата", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	ЕСЛИ НЕ РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;					
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Сообщить("Мало товара " + ВыборкаДетальныеЗаписи.ТоварПредставление + ", необходимо еще " + (- ВыборкаДетальныеЗаписи.КоличествоОстаток))
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры
