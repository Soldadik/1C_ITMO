
Процедура ОбработкаПроведения(Отказ, Режим)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровСписокТоваров.Товар КАК Товар,
		|	СУММА(РеализацияТоваровСписокТоваров.Количество) КАК Количество
		|ПОМЕСТИТЬ ДокТЧ
		|ИЗ
		|	Документ.РеализацияТоваров.СписокТоваров КАК РеализацияТоваровСписокТоваров
		|ГДЕ
		|	РеализацияТоваровСписокТоваров.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровСписокТоваров.Товар
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокТЧ.Товар КАК Товар,
		|	ДокТЧ.Количество КАК Количество
		|ИЗ
		|	ДокТЧ КАК ДокТЧ";
			 
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Движения.ТоварыНаСкладе.Записывать = Истина;
	Движения.ТоварыНаСкладе.БлокироватьДляИзменения = Истина; //не даст одновременно продать один товар (блокировка остатков для других сеансов)
	
	Пока Выборка.Следующий() Цикл
		//Для Каждого ТекСтрокаСписокТоваров Из СписокТоваров Цикл
		Движение = Движения.ТоварыНаСкладе.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Товар = Выборка.Товар;
		Движение.Количество = Выборка.Количество;
	КонецЦикла;
	
	Движения.Записать();
	
	//Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладеОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ТоварыНаСкладеОстатки.Товар.Представление КАК ТоварПредставление
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладе.Остатки(&Дата, Товар В(ВЫБРАТЬ ДокТЧ.Товар ИЗ ДокТЧ) ) КАК ТоварыНаСкладеОстатки
		|ГДЕ
		|	ТоварыНаСкладеОстатки.КоличествоОстаток < 0";
	
	// Чтобы избежать вариантов, когда товар списывается до того, как появится на складе, что приведет к отрицательному значению остатка на складе
	Запрос.УстановитьПараметр("Дата", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	// Чтобы избежать выгрузки ВСЕХ товаров используем массив
	//Запрос.УстановитьПараметр("МассивТоваров", СписокТоваров.ВыгрузитьКолонку("Товар"));
	
	
	РезультатЗапроса = Запрос.Выполнить();
	ЕСЛИ НЕ РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;					
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Сообщить("Мало товара " + ВыборкаДетальныеЗаписи.ТоварПредставление + ", необходимо еще " + (- ВыборкаДетальныеЗаписи.КоличествоОстаток))
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры
