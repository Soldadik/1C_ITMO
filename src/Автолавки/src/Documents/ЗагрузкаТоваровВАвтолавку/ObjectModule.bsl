
Процедура ОбработкаПроведения(Отказ, Режим)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗагрузкаТоваровВАвтолавкуСписокТоваров.Товар КАК Товар,
		|	СУММА(ЗагрузкаТоваровВАвтолавкуСписокТоваров.Количество) КАК Количество,
		|	ЗагрузкаТоваровВАвтолавку.НомерПутевогоЛиста КАК НомерПутевогоЛиста
		|ПОМЕСТИТЬ ДокТЧ
		|ИЗ
		|	Документ.ЗагрузкаТоваровВАвтолавку.СписокТоваров КАК ЗагрузкаТоваровВАвтолавкуСписокТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗагрузкаТоваровВАвтолавку КАК ЗагрузкаТоваровВАвтолавку
		|		ПО ЗагрузкаТоваровВАвтолавкуСписокТоваров.Ссылка = ЗагрузкаТоваровВАвтолавку.Ссылка
		|ГДЕ
		|	ЗагрузкаТоваровВАвтолавкуСписокТоваров.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗагрузкаТоваровВАвтолавкуСписокТоваров.Товар,
		|	ЗагрузкаТоваровВАвтолавку.НомерПутевогоЛиста
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокТЧ.Товар КАК Товар,
		|	ДокТЧ.Количество КАК Количество
		|ИЗ
		|	ДокТЧ КАК ДокТЧ";
			 
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Движения.ТоварыНаСкладе.Записывать = Истина;
	Движения.ТоварыНаСкладе.БлокироватьДляИзменения = Истина;
	
	
	Движения.ТоварыВАвтолавке.Записывать = Истина;
	Движения.ТоварыВАвтолавке.БлокироватьДляИзменения = Истина;
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ТоварыНаСкладе.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Товар = Выборка.Товар;
		Движение.Количество = Выборка.Количество;
		
		Движение = Движения.ТоварыВАвтолавке.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Товар = Выборка.Товар;
		Движение.Количество = Выборка.Количество;
		Движение.ПутевойЛист = НомерПутевогоЛиста;
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладеОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ТоварыНаСкладеОстатки.Товар.Представление КАК ТоварПредставление
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладе.Остатки(&Дата, Товар В(ВЫБРАТЬ ДокТЧ.Товар ИЗ ДокТЧ) ) КАК ТоварыНаСкладеОстатки
		|ГДЕ
		|	ТоварыНаСкладеОстатки.КоличествоОстаток < 0";
	
	
	Запрос.УстановитьПараметр("Дата", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	ЕСЛИ НЕ РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;					
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Сообщить("Мало товара " + ВыборкаДетальныеЗаписи.ТоварПредставление + ", необходимо еще " + (- ВыборкаДетальныеЗаписи.КоличествоОстаток))
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры
